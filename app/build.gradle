apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "ip2location.android.demo"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"

        resValue "string", "google_maps_key", rootProject.ext.GOOGLE_MAPS_API_KEY
        buildConfigField "String", "APPENGINE_ROOT_URL", "\"https://${rootProject.ext.APPENGINE_PROJECT_ID}.appspot.com/_ah/api/\""
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.google.android.gms:play-services-maps:7.8.0'
    compile project(path: ':appengine', configuration: 'android-endpoints')
}

task checkApiKey {
    if(!rootProject.ext.GOOGLE_MAPS_API_KEY.startsWith('AIza'))
        throw new InvalidUserDataException("Before you run this application, you need a Google Maps API key.\n" +
                "To get one, follow this link:\n" +
                "https://console.developers.google.com/flows/enableapi?apiid=maps_android_backend&keyType=CLIENT_SIDE_ANDROID\n" +
                "Once you have your key, fill the GOOGLE_MAPS_API_KEY variable in ${rootProject.file('build.gradle')}.")
}

preBuild.dependsOn checkApiKey
